buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.11.0"
    }
}

apply plugin: 'java'
apply plugin: 'checkstyle'
apply plugin: 'signing'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'io.codearte.nexus-staging'

group 'com.remondis'
version PROJECT_VERSION
description 'A declarative mapping library for converting objects field by field. '

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

// Doclint enforces generic parameter documentations that are redundant.
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

dependencies {
    compile "cglib:cglib:3.2.6"
    compile "org.ow2.asm:asm:6.2"

    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-core:2.18.0"
    testCompile "org.assertj:assertj-core:3.9.1"

    testCompile "org.springframework:spring-test:4.3.16.RELEASE"
    testCompile "org.springframework:spring-context:4.3.16.RELEASE"
}

sourceSets {
    main {
        java {
            srcDir "src/main/java"
        }
    }
}


checkstyle {
    configFile = rootProject.file("shared/config/checkstyle/checkstyle.xml")
    toolVersion = "7.7"
    maxErrors = 0
    maxWarnings = 0
    sourceSets = [project.sourceSets.main]
}


task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

jar {
    manifest {
        attributes("Bundle-Vendor": "REMONDIS IT Services GmbH & Co. KG",
                "Bundle-DocURL": "https://github.com/remondis-it/remap",
                "Automatic-Module-Name": "com.remondis.remap")
    }
}


if (project.hasProperty('release')) {

    // Signature of artifacts
    signing {
        sign configurations.archives
    }

    nexusStaging {
        username = ossrhUser
        password = ossrhPassword
    }

    /*---------------  Modulepath for Jigsaw  ------------------*/
//"Copied" from: https://guides.gradle.org/building-java-9-modules/
    ext.moduleName = 'com.remondis.azubi.test.jigsaw.otherHead'

// 	Clears the classpath property by creating an empty file collection
    compileJava {
        inputs.property("moduleName", moduleName)
        doFirst {
            options.compilerArgs = [
                    '--module-path', classpath.asPath,
            ]
            classpath = files()
        }
    }

//Redifines the classpath/modulepath
    compileTestJava {
        inputs.property("moduleName", moduleName)
        doFirst {
            options.compilerArgs = [
                    '--module-path', classpath.asPath,
                    '--add-modules', 'junit',
                    '--add-reads', "$moduleName=junit",
                    '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath,
            ]
            classpath = files()
        }
    }

    test {
        inputs.property("moduleName", moduleName)
        doFirst {
            jvmArgs = [
                    '--module-path', classpath.asPath,
                    '--add-modules', 'ALL-MODULE-PATH',
                    '--add-reads', "$moduleName=junit",
                    '--patch-module', "$moduleName=" + files(sourceSets.test.java.outputDir).asPath,
            ]
            classpath = files()
        }
    }

    subprojects {
        afterEvaluate {
            repositories {
                jcenter()
            }

            compileJava {
                inputs.property("moduleName", moduleName)
                doFirst {
                    options.compilerArgs = [
                            '--module-path', classpath.asPath,
                    ]
                    classpath = files()
                }
            }

            compileTestJava {
                inputs.property("moduleName", moduleName)
                doFirst {
                    options.compilerArgs = [
                            '--module-path', classpath.asPath,
                            '--add-modules', 'junit',
                            '--add-reads', "$moduleName=junit",
                            '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath,
                    ]
                    classpath = files()
                }
            }

            test {
                inputs.property("moduleName", moduleName)
                doFirst {
                    jvmArgs = [
                            '--module-path', classpath.asPath,
                            '--add-modules', 'ALL-MODULE-PATH',
                            '--add-reads', "$moduleName=junit",
                            '--patch-module', "$moduleName=" + files(sourceSets.test.java.outputDir).asPath,
                    ]
                    classpath = files()
                }
            }
        }
    }
/*----------------------------------------------*/

    // OSSRH publication
    uploadArchives {
        repositories {
            mavenDeployer {
                // POM signature
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUser, password: ossrhPassword)
                }
                pom.project {
                    name project.name
                    description project.description
                    packaging 'jar'
                    url = 'https://github.com/remondis-it/remap'

                    scm {
                        connection 'scm:git:https://github.com/remondis-it/remap.git'
                        developerConnection 'scm:git:git@github.com:remondis-it/remap.git'
                        url 'https://github.com/remondis-it/remap.git'
                    }

                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                        }
                    }

                    developers {
                        developer {
                            name = 'Christopher Sch√ºtte'
                            email = 'christopher.schuette@remondis.de'
                            organization = 'REMONDIS IT Services GmbH & Co. KG'
                        }
                    }
                }
            }
        }
    }
}
